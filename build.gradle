/*
 *
 *
 *          Copyright (c) 2024. - TinyZ.
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */
plugins {
    id 'java'
}

ext.pack_version = (hasProperty("pack_version") ? property("pack_version") : version)

group = 'org.structutil'
version = '4.1.0.beta-SNAPSHOT'
description = 'Light Flexible And High Performance Java Bean Struct Data Mapper.'

ext.struct_jvm_projects = [
        ':struct-all',
        ':struct-core',
        ':struct-spring',
        ':struct-spring-boot-starter'
]

allprojects {
    apply plugin: 'jacoco'
    apply plugin: 'java-library'

    ext.version_options = [
            SPRING_BOOT_VERSION: "3.1.5",
            SPRING_VERSION     : "6.0.13",
            JETTY_VERSION      : "9.4.44.v20210927",
            MOCKITO_VERSION    : "5.7.0",
            JUNIT_VERSION      : "5.9.3",
            POI_VERSION        : "5.2.2",
            LOG4J2_VERSION     : "2.20.0",
            GSON_VERSION       : "2.9.0",
    ]

    jacoco {
        toolVersion = "0.8.8"
    }

    repositories {
        mavenLocal()
        maven { url "https://repo1.maven.org/maven2/" }
        maven { url "https://repo.maven.apache.org/maven2/" }
        maven { url "https://repo.typesafe.com/typesafe/repo" }
        maven { url "https://maven.aliyun.com/repository/central" }
        maven { url "https://maven.aliyun.com/repository/spring" }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'distribution'

    sourceCompatibility = 17
    targetCompatibility = 17
    compileJava.options.setEncoding("UTF-8")
    compileTestJava.options.setEncoding("UTF-8")

    group = "${rootProject.group}"
    version = "${rootProject.version}"

    dependencies {
        //  slf4j
        implementation('org.slf4j:slf4j-api:2.0.9')
        //  junit 5.x and mockito
        testImplementation("org.mockito:mockito-core:${version_options.MOCKITO_VERSION}")
        testImplementation("org.junit.jupiter:junit-jupiter:${version_options.JUNIT_VERSION}")
        //  log4j 2.x
        testImplementation("org.apache.logging.log4j:log4j-api:${version_options.LOG4J2_VERSION}")
        testImplementation("org.apache.logging.log4j:log4j-core:${version_options.LOG4J2_VERSION}")
        testImplementation("org.apache.logging.log4j:log4j-slf4j2-impl:${version_options.LOG4J2_VERSION}")
        testImplementation("org.apache.logging.log4j:log4j-jul:${version_options.LOG4J2_VERSION}")
    }

    test {
        ignoreFailures = true
        useJUnitPlatform()
    }

    distTar {
        setArchivesBaseName("${project.name}")
        compression = Compression.GZIP
        getArchiveExtension().set('tar.gz')
    }

    distributions {
        main {
            contents {
                from project.tasks.withType(Jar)
            }
        }
    }
}

/// <editor-fold desc="  Code Coverage  ">

ext.codecovProjects = subprojects.findAll {
    return ["struct-core", "struct-spring", "struct-spring-boot-starter"].contains(it.name)
}
task codeCoverageReport(type: JacocoReport) {
    getExecutionData().setFrom(fileTree(dir: project.rootDir.absolutePath).include("**/build/jacoco/*.exec"))

    project.ext.codecovProjects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/report.xml")
        html.enabled false
        html.destination file("${buildDir}/reports/html")
        csv.enabled false
    }

    afterEvaluate {
        getClassDirectories().setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: [
                            '**/bootstrap/**'
                    ])
        }))
    }

    //  jacocoRootReport doesn't work if some subprojects don't have any tests at all because this causes the onlyIf of JacocoReport to be false.
    onlyIf = { true }
}
codeCoverageReport.dependsOn {
    ext.codecovProjects*.test
}
/// </editor-fold>


//  <editor-fold desc="   Maven Publish  Local ">
//  https://docs.gradle.org/current/userguide/publishing_maven.html#publishing_maven:install

Closure init_publish_module = {

    apply plugin: 'maven-publish'

    task generatePomPropertiesFile {
        inputs.properties([
                'version'   : "${-> project.version}",
                'groupId'   : "${-> project.group}",
                'artifactId': "${-> project.name}"
        ])
        outputs.file "$project.buildDir/generatePomPropertiesFile/pom.properties"
        doLast {
            File file = outputs.files.singleFile
            if (file.isFile() && file.exists()) {
                file.delete()
            }
            file.parentFile.mkdirs()
            file << inputs.properties.collect { key, value -> "$key: $value" }.join('\n')
        }
    }

    model {
        tasks.jar {
            manifest {
                attributes 'Implementation-Title': project.description ?: rootProject.description,
                        'Implementation-Version': project.version,
                        'Built-By': System.getProperty('user.name'),
                        'Built-Date': new Date(),
                        'Built-JDK': System.getProperty('java.version'),
                        'Built-Gradle': gradle.gradleVersion,
                        'Target-JDK': project.targetCompatibility
            }
            into("META-INF/maven/$project.group/$project.name") {
                from { generatePomFileForMavenPublication }
                rename ".*.xml", "pom.xml"
                from generatePomPropertiesFile
            }
        }
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        archiveClassifier.set('sources')
    }

    task javadocJar(type: Jar) {
        from javadoc {
            options.encoding = 'UTF-8'
            options.tags = ["date"]
            failOnError = false
        }
        archiveClassifier.set('javadoc')
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar

                pom {
                    name = "$project.name"
                    description = "${project.description ?: rootProject.description}"
                    url = "https://github.com/TinyZzh/StructUtil"
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    issueManagement {
                        url = 'https://github.com/TinyZzh/StructUtil/issues'
                    }
                    developers {
                        developer {
                            id = "TinyZ"
                            name = "TinyZzh"
                            email = "tinyzzh815@gmail.com"
                        }
                    }
                    withXml {
//                        asNode().appendNode('dependencies')
//                                .appendNode('dependency')
//                                .appendNode('scope', 'compile')
                    }
                }
            }
        }
        repositories {
            mavenLocal()
            maven {
                name "local_nexus"
                url project.findProperty("mavenRepoUrl") ?: ""
                credentials {
                    username = project.findProperty("mavenRepoUser") ?: ""
                    password = project.findProperty("mavenRepoPassword") ?: ""
                }
                allowInsecureProtocol = true
            }
        }
    }
}
ext.struct_jvm_projects.each {
    project(it, init_publish_module)
}
/// </editor-fold>
